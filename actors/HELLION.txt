Actor Hellion 30132
{
  Obituary "A Hellion burned %o to a crisp."
  Health 70
  Radius 20
  Height 56
  Mass 120
  Speed 10
  PainChance 24
  SeeSound "monster/hlnsit"
  PainSound "monster/hlnpai"
  DeathSound "monster/hlndth"
  ActiveSound "monster/hlnact"
  Monster
  +FloorClip
  States
  {
  Spawn:
    HELN AB 10 A_Look
    Loop
  See:
    HELN AABBCCDDEEFF 2 A_Chase
    Loop
  Missile:
    HELN G 0 A_VileStart
    HELN GHIJK 6 Bright A_FaceTarget
    HELN L 6 Bright A_CustomMissile("HellionBall",32,0,0,0,0)
    HELN L 2
    Goto See
  Pain:
    HELN M 2
    HELN M 2 A_Pain
    Goto See
  Death:
    HELN N 6
    HELN O 6 A_Scream
    HELN PQR 6
    HELN S 6 A_NoBlocking
    HELN T -1
    Stop
  XDeath:
    HELN U 5
    HELN V 5 A_XScream
    HELN W 5
    HELN X 5 A_NoBlocking
    HELN YZ 5
    HEL2 AB 5
    HEL2 C -1
    Stop
  Raise:
    HELN TSRQPON 6
    Goto See
  }
}

Actor HellionBall : CacodemonBall
{
  Damage 8
  Speed 15
  Alpha 0.80
  DamageType Fire
  +ThruGhost
  +ForceXYBillboard
  SeeSound "Monster/hlnatk"
  DeathSound "Monster/hlnexp"
  Decal DoomImpScorch
  states
  {
  Spawn:
    HLBL AB 3 Bright A_SpawnItemEx("HellionPuff",0,0,0,0,0,0,0,128)
    Loop
  Death:
    HLBL JKLMN 3 Bright
    Stop
  }
}

Actor HellionPuff
{
  Radius 3
  Height 3
  RenderStyle Add
  Alpha 0.67
  +NoGravity
  +NoBlockMap
  +DontSplash
  +ForceXYBillboard
  States
  {
  Spawn:
    TNT1 A 3 Bright
    HLBL CDEFGHI 3 Bright
    Stop
  }
}

actor HellionSpot: CustomMonsterInvasionSpot 14687
{
	//$Category invasion_spots
	//$Sprite HELNA2D8
	dropitem Hellion
}